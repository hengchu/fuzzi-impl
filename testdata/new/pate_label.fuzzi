types

/* input data to be labeled */
db_test : [ [float(785)](100) ];
/* trained PATE models */
models :[1.0] { [float(785)] };
/* output labels for 1 data point*/
labels : { float };
label_sum : float;

outputs : [float(100)];

/* aux variables to be used in bag map */
t_model : [float(785)];
i : int;
t_label : float;
j : int;
k : int;
t_db_test  : [float(785)];
t_db_test1 : [float(785)];

zero_785 : [float(785)];
zero_786 : [float(786)];

NUM_PARTITIONS : float;

end

NUM_PARTITIONS = 5.0;

i = 0;
repeat(i, 100,
  t_db_test = db_test[i];
  t_db_test1[0] = 1.0;
  repeat(j, 784,
    t_db_test1[j+1] = t_db_test[j];
  );

  bmap(models, labels, t_model, k, t_label,
    t_label = dot(t_model, t_db_test1);
    if t_label > 0.0 then
      t_label = 1.0
    else
      t_label = -1.0
    end
  );

  bsum(labels, label_sum, k, t_label, 1.0);
  label_sum $= lap(5.0, label_sum);
  label_sum = label_sum / NUM_PARTITIONS;

  if label_sum > 0.0 then
    outputs[i] = 1.0
  else
    outputs[i] = -1.0
  end
);
